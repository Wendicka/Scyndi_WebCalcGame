{ --- START LICENSE BLOCK ---
	Web Calculation Game
	Tasks themselves
	
	
	
	(c) Jeroen P. Broks, 2019
	
		This program is free software: you can redistribute it and/or modify
		it under the terms of the GNU General Public License as published by
		the Free Software Foundation, either version 3 of the License, or
		(at your option) any later version.
		
		This program is distributed in the hope that it will be useful,
		but WITHOUT ANY WARRANTY; without even the implied warranty of
		MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
		GNU General Public License for more details.
		You should have received a copy of the GNU General Public License
		along with this program.  If not, see <http://www.gnu.org/licenses/>.
	
	Please note that some references to data like pictures or audio, do not automatically
	fall under this licenses. Mostly this is noted in the respective files.
		
Version: 19.02.15
  --- END LICENSE BLOCK --- } 

import "document.getElementById('tasknum').innerHTML" var TaskCaption:String
import "document.getElementById('sommetje').innerHTML" var Sommetje:String
import "document.getElementById('resultaat').innerHTML" var resultaat:String

void Create_Sommetje
	var cijfer1:integer
	var cijfer2:integer
	ignoreanswer = 1=2 {must be false... The compiler is bugged when I just put in false}
	resultaat=""
	task++
	if task>tasks
		WriteLn "The end's been reached!"
		GotoEinde
		return
	end
	ModifyElement "antwoord","value",""
	TaskCaption = "Task "+task+" of "+tasks
	switch somtype
		case 1
			{tafeltjes}
			Sommetje      = ""+task+" * "+cnum+" = "
			correctanswer = task*cnum
		case 2
			{optellen}
			cijfer1       = Rand(1,cnum)
			cijfer2       = Rand(1,cnum)
			sommetje      = ""+cijfer1+" + "+cijfer2+" = "
			correctanswer = cijfer1 + cijfer2
		case 3
			{min sommen}
			cijfer1	   = rand(1,cnum)
			cijfer2       = rand(1,ceil(cijfer1/2))
			sommetje      = ""+cijfer1+" - "+cijfer2+" = "
			correctanswer = cijfer1 - cijfer2
		case 4
			{delen}
			repeat
				correctanswer = rand(1,cnum)
				cijfer2       = rand(2,correctanswer)
				cijfer1       = cijfer2 * correctanswer
			until cijfer1<=cnum
			sommetje = ""+cijfer1+" : "+cijfer2+" = "
		case 5
			{vermenigvuldigen}
			repeat
				cijfer1       = rand(1,cnum)
				cijfer2       = rand(1,cnum)
				correctanswer = cijfer1 * cijfer2
			until correctanswer<=cnum
			sommetje = ""+cijfer1+" x "+cijfer2+" = "
		default; annoy "Unknown task type"
	end
	ModifyElement "portraitimg","src",pic_thinking
end

void CheckAnswer
	writeln "User requested to check their answer."
	if ignoreanswer; return; end
	ignoreanswer=1=1 {Must be true, compiler bug on true and false definitions... How odd}
	var isnum:boolean
	var ant:integer
	isnum = ValueFromElementIsNumber("antwoord")
	if not isnum
		annoy "Please enter a NUMBER only!"
	end
	ant = IntFromElement("antwoord")
	if ant=correctanswer
		resultaat = "<span style='color:rgb(180,255,0)'>Correct! Well done!</span>"
		ModifyElement "portraitimg","src",pic_happy
		r_right++
	else
		resultaat = "<span style='color:rgb(255,0,0)'>Unfortunately, that not right!<br />The correct answer is <span='style:rgb(0,180,255)'>"+correctanswer+"</span></span>"
		ModifyElement "portraitimg","src",pic_sad
		r_wrong++
	end
	purecode "WebJS","setTimeout($C{CREATE_SOMMETJE}C$, $C{BASIC_TIME}C$);"
end

void GoToSommetjes
	content = "<table><caption id='tasknum'></caption>
	<tr>
		<td id='portretcell' rowspan=2 align=left'></td>
		<td><span id='sommetje' style='color:rgb(180,0,255);'></span><input style='color:rgb(0,180,255); background-color:rgb(0,18,25);' id='antwoord'><input id='bevestig' value='>>' type='submit'></td>
	</tr>
	<tr>
		<td id='resultaat'></td>
	</tr>
</table>"
	H_Flush
	H_CreatePortrait "portretcell"
	task    = 0
	r_right = 0
	r_wrong = 0
	Create_Sommetje
	purecode "WebJS","document.getElementById('bevestig').addEventListener( 'click', $C{CHECKANSWER}C$ );"
end
